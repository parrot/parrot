# Copyright (C) 2001-2003 The Perl Foundation.  All rights reserved.
# $Id$

=head1 NAME

examples/assembly/pcre.imc - String matching

=head1 SYNOPSIS

    % ./parrot examples/assembly/pcre.imc string pattern

=head1 DESCRIPTION

Experimental string matching with PCRE (L<http://www.pcre.org/>).

Note that PCRE must be installed for this to work.

=cut

.pcc_sub _main prototyped
    .param pmc argv
.include "library/pcre.imc"
    .sym int argc
    argc = argv
    if argc != 3 goto usage

    .sym string s
    .sym string pat
    s = argv[1]
    pat = argv[2]
    print s
    print " =~ /"
    print pat
    print "/\n"
    .sym pmc PCRE_LIB
    .PCRE_INIT(PCRE_LIB)
    .sym string error
    .sym int errptr
    .sym pmc code
    .PCRE_COMPILE(pat, 0, code, error, errptr)
    $I0 = defined code
    unless $I0 goto match_err

    .sym int ok
    .sym pmc result
    .PCRE_MATCH(code, s, 0, 0, ok, result)
    if ok < 0 goto nomatch
    print ok
    print " match(es):\n"
    .sym int i
    i = 0
    .sym string match
lp: .PCRE_DOLLAR(s, ok, result, i, match)
    print match
    print "\n"
    inc i
    if i < ok goto lp
    end
nomatch:
    print "no match\n"
    end
match_err:
    print "error in regex: "
    #print error
    print "at: '"
    length $I0, pat
    $I0 = $I0 - errptr
    substr $S0, pat, errptr, $I0
    print $S0
    print "'\n"
    exit 1
usage:
    .sym string prog
    prog = argv[0]
    print prog
    print " string pattern\n"
    exit 1
.end

=head1 SEE ALSO

F<library/libpcre.imc>, F<library/pcre.imc>.

=cut
