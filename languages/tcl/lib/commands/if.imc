###
# [if]

.namespace [ "Tcl" ]

.sub "if"
  .local pmc argv 
  argv = foldup

  .local int argc 
  argc = argv

  .local int TclList
  TclList = find_type "TclList"

  .local pmc elseifs
  elseifs = new TclList
  .local pmc retval,condition
  .local string code
  .local pmc condition
  .local string body
  .local string else
  .local int return_type

  .local int counter

  .local pmc parser,interpret
  .local pmc expression_p,expression_i
  parser = find_global "_Tcl", "parser"
  interpret = find_global "_Tcl", "__interpret"
  expression_p = find_global "_Tcl", "__expression_parse"
  expression_i = find_global "_Tcl", "__expression_interpret"
 
  .local string temp_str
  temp_str ="" 

  # XXX each one of this argv[counter] sets should be protected
  # against getting past the end of the input array... 
 
  counter = 0
  condition = argv[counter]
  inc counter
  body = argv[counter]
  inc counter
  if body != "then" goto get_elseifs 
  body = argv[counter]
  inc counter
get_elseifs:
  if counter >= argc goto get_final
  temp_str = argv[counter]
  if temp_str != "elseif" goto get_else
  $P1 = new TclList
  inc counter
  $P2 = argv[counter]
  $P1[0] = $P2
  inc counter
  $P2 = argv[counter]
  $P1[1] = $P2
  push elseifs, $P1
  inc counter
  goto get_elseifs
get_else:
  temp_str = argv[counter]
  if temp_str != "else" goto get_final
  inc counter
  temp_str = argv[counter]
get_final:
  else = temp_str

  (return_type,retval) = expression_p(condition)
  if return_type == TCL_ERROR goto done_error
  (return_type,retval) = expression_i(retval)
  if return_type == TCL_ERROR goto done_error

  unless retval goto do_elseifs
  code = body 
  goto done
 
do_elseifs:
  $I1 = elseifs
  if $I1 == 0 goto do_else
  $I2 = 0
elseif_loop:
  if $I2 == $I1 goto do_else
  $P1 = elseifs[$I2]
  condition = $P1[0]
  (return_type,retval) = expression_p(condition)
  if return_type == TCL_ERROR goto done_error
  (return_type,retval) = expression_i(retval)
  if return_type == TCL_ERROR goto done_error
  if retval goto done_elseifs
  inc $I2
  goto elseif_loop  

done_elseifs:
  code = $P1[1]
  goto done

do_else:
  code = else

done:
  .local pmc zero
  zero = new Integer
  .local pmc pmcString
  pmcString = new String
  pmcString = code
 
  $P1 = parser."parse"(pmcString,zero,zero)

  .return interpret($P1) #tailcall

done_error:
  .return(return_type,retval)
.end
