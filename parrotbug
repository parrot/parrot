#!/usr/bin/env perl
#
# $Id$
#

eval 'exec perl -w -S $0 ${1+"$@"}'
  if $running_under_some_shell;

#
# This utility borrows heavily from perlbug.
#

use Config;
use File::Spec;
use Getopt::Std;

my $VERSION = "0.0.1";

my %std_to = 
  ( bug => 'parrotbug@parrotcode.org',
    ok  => 'parrotstatus-ok@parrotcode.org',
    nok => 'parrotstatus-nok@parrotcode.org'
  );

my $parrotdir = File::Spec->curdir();

my ( $user, $domain );
my ( $to, $cc, $from, $subject, $msgid, $body );
my ( $category, $severity );
my ( $is_linux, $is_macos, $is_mswin32, $is_os2, $is_vms );
my ( $parrot_version, $parrot_myconfig ) ;


#------------------------------------------------------------#
#                       Main program.                        #
BEGIN {
    eval "use Mail::Send;";
    $have_send = ($@ eq "");
}

init();
help()    if $opt_h;
version() if $opt_V;
query_info();

die "work in progress... nothing ready yet!\n";

edit_info();
ask_for_action();
send_msg();
exit;




#------------------------------------------------------------#
#                           Subs.                            #


# Print synopsis + help message and exit.
sub help {
    print <<EOF;

A program to help generate bug reports about parrot, and mail them.

Usage:

 $0 [-s subject] [-b body] [-f inputfile] [-r returnaddress] [-A]
    [-ok|-nok]
 $0 {-h|-V}

Options:
  -A    Don't send a bug received acknowledgement to the return address.
  -b    Body of the report. If not included on the command line, or
        in a file with -f, you will get a chance to edit the message.
  -f    File containing the body of the report. Use this to
        quickly send a prepared message.
  -h    Print this help message and exit.
  -nok  Report unsuccessful build on this system to parrot developpers
  -ok   Report successful build on this system to parrot developpers
        Only use -ok if *everything* was ok: if there were *any* problems
        at all, use -nok.
  -r    Your return address. The program will ask you to confirm
        this if you don't give it here.
  -s    Subject to include with the message. You will be prompted
        if you don't supply one on the command line.
  -V    Print version information and exit.

EOF
#'
    exit;
}


# Initialize prog:
#  - get parrot information
#  - process options
#  - create the message information (subject, to, body, etc.)
#    depending on the type of report (ok, nok or bug report)
sub init {
    $is_linux   = lc($^O) eq 'linux';
    $is_macos   = $^O eq 'MacOS';
    $is_mswin32 = $^O eq 'MSWin32';
    $is_os2     = $^O eq 'os2';
    $is_vms     = $^O eq 'VMS';

    ##
    ## Fetch Parrot information.
    ##

    # Get parrot version.
    # There will always be an up-to-date $parrot/VERSION
    my $filename = File::Spec->catfile($parrotdir, "VERSION");
    open(VERSION, "<$filename") or die "Cannot open '$filename': $!";
    $parrot_version = <VERSION>;
    chomp($parrot_version);
    close(VERSION) or die "Cannot close '$filename': $!";

    # Get parrot configuration, stored in $parrot/myconfig
    $filename = File::Spec->catfile($parrotdir, "myconfig");
    open(MYCONFIG, "<$filename") or die "Cannot open '$filename': $!";
    {
        local $/;
        $parrot_myconfig = <MYCONFIG>;
    }
    close(MYCONFIG) or die "Cannot close '$filename': $!";


    ##
    ## Process options.
    ##
    @ARGV = split m/\s+/,
        MacPerl::Ask('Provide command-line args here (-h for help):')
        if $is_macos && $MacPerl::Version =~ /App/;
    help() unless getopts("AVhb:f:n:o:r:s:");


    ##
    ## Report to be sent.
    ##
  sw: {
      opt_o: {
            last opt_o unless defined $opt_o;
            help()     unless $opt_o eq "k";

            # This is an ok report, woohoo!
            $to = $std_to{ok};
            $subject = "OK: parrot $parrot_version "
              . "on $Config{archname} $Config{osvers}";
            $body = "Parrot reported to build OK on this system.\n";
            $category = "install";
            $severity = "none";
            last sw;
        };

        # Ok reports do not need body, but nok and bug reports do need
        # a body. It can be done with either -f or -b flag.
        if ( $opt_f ) {
            # A file was provided.
            open(BODY, "<$opt_f") or die "Cannot open '$opt_f': $!";
            {
                local $/;
                $body = <BODY>;
            }
            close(BODY) or die "Cannot close '$opt_f': $!";
        } else {
            # No file provided...
            $body = $opt_b || "";
        }
            

      opt_n: {
            last opt_n unless defined $opt_n;
            help()     unless $opt_n eq "ok";

            # This a nok report, how sad... :-(
            $to = $std_to{nok};
            $subject = "Not OK: parrot $parrot_version "
              . "on $Config{archname} $Config{osvers}";
            $category = "install";
            $severity = "none";
            last sw;
        };

        # Neither an ok nor a nok.
        $to = $std_to{bug};
        $subject  = $opt_s || "";
        $category = "";
        $severity = "";
    };


    ## 
    ## Mail information.
    ## 

    # Username.
    $user = $is_mswin32 ? $ENV{'USERNAME'}
	    : $is_os2   ? $ENV{'USER'} || $ENV{'LOGNAME'}
	    : $is_macos ? $ENV{'USER'}
	    : eval { getpwuid($<) };	# May be missing

    # User address, used in message and in Reply-To header.
    $from = $opt_r || "";

    # Message-Id.
    eval "use Mail::Util;";
    if ( $@ eq "" ) {
        $domain = Mail::Util::maildomain();
    } elsif ($is_mswin32) {
        $domain = $ENV{'USERDOMAIN'};
    } else {
        require Sys::Hostname;
        $domain = Sys::Hostname::hostname();
    }
    $msgid = "<parrotbug_$VERSION_$$_".time."\@$domain>";
}


# Query missing information in order to have a complete report.
# The information collected are:
#  
sub query_info {
    ##
    ## Explain what parrotbug is.
    ##
    print <<EOF;

This program provides an easy way to create a message reporting a bug
in parrot, and e-mail it to parrot developpers.

It is *NOT* intended for:
  - sending test messages,
  - or reporting bugs in languages targetting parrot,
  - or reporting bugs in some library bindings for parrot,
  - or simply verifying that parrot works.

It is *ONLY* a mean of reporting verifiable problems with the core
parrot distribution, and any solutions to such problems, to parrot
developpers.

If you're just looking for help with parrot, subscribe to the parrot
mailing list, perl6-internals<at>perl.org.

EOF
#'

    ##
    ## Prompt for subject of message, if needed.
    ##
    $subject = "" if trivial_subject( $subject );
    unless ( $subject ) {
        print <<EOF;

First of all, please provide a subject for the message. It should be a
concise description of the bug or problem. "parrot bug" or "parrot
problem" is not a concise description.

EOF

        my $err = 0;
        do {
            $err and print "\nThat doesn't look like a good subject. "
              . "Please be more verbose.\n";
            print "Subject: ";
            $subject = <>;
            chomp $subject;
            die "Aborting.\n" if $err++ == 5;
        } while ( trivial_subject($subject) );
    }


    ##
    ## Prompt for return address, if needed.
    ## 
    unless ( $from ) {
        print <<EOF;


Your e-mail address will be useful if you need to be contacted. If the
default shown below is not your full internet e-mail address, please
correct it.
EOF

	# Try and guess return address
        my $guess;
        if ( $is_macos ) {
            require Mac::InternetConfig;
            $guess = $Mac::InternetConfig::InternetConfig{
                Mac::InternetConfig::kICEmail()
            };
        } else {
            $guess = $ENV{REPLY-TO} || $ENV{REPLYTO} || "";
        }

	if ( ! $guess ) {
            # Use $domain if we can.
            if ( $domain ) {
                $guess = $is_vms && !$Config{d_socket} ?
                  "$domain\:\:$user" : "$user\@$domain";
	    }
	}

        # Verify our guess.
        print "Your address [$guess]: ";
        $from = <>;
        chomp $from;
        $from = $guess if $from eq '';
    }
}


sub send_msg {
    # Message has been accepted for transmission -- Send the message
    if ($outfile) {
	open SENDMAIL, ">$outfile" or die "Couldn't open '$outfile': $!\n";
	goto sendout;
    }

    # on linux certain mail implementations won't accept the subject
    # as "~s subject" and thus the Subject header will be corrupted
    # so don't use Mail::Send to be safe
    if ($have_send && !$is_linux) {
	my $msg = new Mail::Send Subject => $subject, To => $address;
	$msg->add("Reply-To",$from) if $from;

	$fh = $msg->open;
        print $fh $body;
	$fh->close;

	print "\nMessage sent.\n";

    } else {
	my $sendmail = "";
	for (qw(/usr/lib/sendmail /usr/sbin/sendmail /usr/ucblib/sendmail)) {
	    $sendmail = $_, last if -e $_;
	}

	die <<"EOF" if $sendmail eq "";
I am terribly sorry, but I cannot find sendmail, or a close
equivalent, and the perl package Mail::Send has not been installed, so
I can't send your bug report. We apologize for the inconvenience.

So you may attempt to find some way of sending your message, it has
been left in the file '$filename'.
EOF
# '
	open(SENDMAIL, "|$sendmail -t -oi") || die "'|$sendmail -t -oi' failed: $!";

	print SENDMAIL "To: $address\n";
	print SENDMAIL "Subject: $subject\n";
	print SENDMAIL "Reply-To: $from\n" if $from;
	print SENDMAIL "Message-Id: $messageid\n" if $messageid;
	print SENDMAIL "\n\n";
        print SENDMAIL $body;

	if (close(SENDMAIL)) {
	    printf "\nMessage sent.\n";
	} else {
	    warn "\nSendmail returned status '", $? >> 8, "'\n";
	}
    }
}


# Check whether a subject is trivial. A subject is not considered
# trivial if it's an ok or a nok report.
# Return 1 if trivial, 0 otherwise (subject acceptable).
sub trivial_subject {
    my $subject = shift;

    return 0 if $opt_o || $opt_f;

    if ( $subject =~
         /^(y(es)?|no?|help|parrot( (bug|problem))?|bug|problem)$/i ||
         length($subject) < 4 ||
         $subject !~ /\s/ ) {
        return 1;
    } else {
	return 0;
    }
}

# Print version information (of the parrotbug program) and exit.
sub version {
    print <<"EOF";

This is $0, version $VERSION.

EOF

    exit;
}
